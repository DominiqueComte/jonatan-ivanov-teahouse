ext.springBootVersion = '2.2.5.RELEASE'
ext.springCloudVersion = 'Hoxton.SR3'
ext.springDataVersion = 'Moore-SR5'

task clean
task bootRun

apply from: "$rootDir/gradle/idea.gradle"

allprojects {
    apply from: "$rootDir/gradle/dependency-locking.gradle"

    task cleanAll {
        dependsOn clean, cleanIdea, cleanIdeaWorkspace
    }
}

subprojects {
    buildscript {
        repositories {
            maven { url 'https://plugins.gradle.org/m2/' }
            mavenCentral()
        }
        dependencies {
            classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
            classpath 'gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:2.2.2'
        }
    }

    apply plugin: 'java'

    sourceCompatibility = 13
    targetCompatibility = 13

    group = 'org.example.teahouse'
    version = '0.0.1-SNAPSHOT'

    ext.profile = project.hasProperty('profile') ? project.getProperty('profile') : 'local' //./gradlew bootRun -Pprofile=profileName

    repositories {
        mavenCentral()
    }

    dependencies {
        ['annotationProcessor', 'compileOnly', 'implementation', 'runtimeOnly', 'testImplementation'].each { conf ->
            dependencies.add(conf, platform("org.springframework.boot:spring-boot-dependencies:$springBootVersion"))
            dependencies.add(conf, platform("org.springframework.cloud:spring-cloud-dependencies:$springCloudVersion"))
            dependencies.add(conf, platform("org.springframework.data:spring-data-releasetrain:$springDataVersion"))
        }
        testImplementation platform('org.junit:junit-bom:5.+')

        annotationProcessor 'org.projectlombok:lombok'
        compileOnly 'org.projectlombok:lombok'

        testImplementation 'org.junit.jupiter:junit-jupiter-api'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    }

    test {
        useJUnitPlatform()
    }
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}
