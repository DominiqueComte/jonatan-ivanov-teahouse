allprojects {
    apply plugin: 'idea'

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true

            iml.withXml { // Spring Facet
                def moduleRoot = it.asNode()
                def facetManager = moduleRoot.component.find { component -> component.'@name' == 'FacetManager' }
                if (!facetManager) {
                    facetManager = moduleRoot.appendNode('component', [name: 'FacetManager'])
                }
                def springFacet = facetManager.facet.find { facet -> facet.'@type' == 'Spring' && facet.'@name' == 'Spring' }
                if (!springFacet) {
                    springFacet = facetManager.appendNode('facet', [type: 'Spring', name: 'Spring'])
                    springFacet.appendNode('configuration')
                }
            }
        }
    }
}

idea {
    project.ipr.withXml { provider -> // Annotation processor
        def compilerConfiguration = provider.asNode().component.find { it.'@name' == 'CompilerConfiguration' }
        compilerConfiguration.annotationProcessing.replaceNode {
            annotationProcessing { profile(default: true, name: 'Default', enabled: true) }
        }
    }
}
